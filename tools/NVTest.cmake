# CMake module
include(CMakeParseArguments)

function(join_quoted ARG_IN ARG_OUT)
  list(GET ARG_IN 0 ARG_0)
  set(TMP "\"${ARG_0}\"")
  list(LENGTH ARG_IN N)
  if(N GREATER 1)
    list(SUBLIST ARG_IN 1 -1 ARG_X)
    foreach(ARG_I ${ARG_X})
      string(APPEND TMP ", \"${ARG_I}\"")
    endforeach()
  endif()
  set(${ARG_OUT} "${TMP}" PARENT_SCOPE)
endfunction()

# --- ADD_NVTEST --------------------------------------------------------------------- #
function(add_nvtest)
  macro(_print_usage)
    set(USAGE_STR "\nADD_NVTEST(NAME <name> <COMMAND <command>|SCRIPT <script>> ")
    string(APPEND USAGE_STR "[NO_DEFAULT_LINK] [LINK link1 [link2...]])\n")
    string(APPEND USAGE_STR "[KEYWORDS kwd1 [kwd2...]])\n")
    string(APPEND USAGE_STR "[DEPENDS_ON dep1 [dep2...]])\n")
    string(APPEND USAGE_STR "Add a nvtest unit test.\n")
    message("${USAGE_STR}")
  endmacro(_print_usage)
  cmake_parse_arguments(PARSED_ARGS "NO_DEFAULT_LINK" "NAME;COMMAND;SCRIPT" "LINK;KEYWORDS;DEPENDS_ON" ${ARGN})
  if(NOT PARSED_ARGS_NAME)
    _print_usage()
    message(FATAL_ERROR "Must define <name>")
  endif()
  set(NAME ${PARSED_ARGS_NAME})

  if(NOT PARSED_ARGS_COMMAND AND NOT PARSED_ARGS_SCRIPT)
    _print_usage()
    message(FATAL_ERROR "Must define exactly one of <command> or <script>")
  endif()

  if(PARSED_ARGS_COMMAND AND PARSED_ARGS_SCRIPT)
    _print_usage()
    message(FATAL_ERROR "Must define exactly one of <command> or <script>")
  endif()

  if(PARSED_ARGS_COMMAND)
    set(CONTENT "#!/usr/bin/env python3\nimport sys\nimport nvtest\n")
    separate_arguments(COMMAND_LIST UNIX_COMMAND ${PARSED_ARGS_COMMAND})
    list(GET COMMAND_LIST 0 PROGRAM)
    if(PARSED_ARGS_KEYWORDS)
      join_quoted("${PARSED_ARGS_KEYWORDS}" KWDS)
      string(APPEND CONTENT "nvtest.directives.keywords(${KWDS})\n")
    endif()
    if(NOT NO_DEFAULT_LINK)
      string(APPEND CONTENT "nvtest.directives.link(\"${PROGRAM}\")\n")
    endif()
    if(PARSED_ARGS_DEPENDS_ON)
      foreach(DEP ${DEPENDS_ON})
        string(APPEND CONTENT "nvtest.directives.depends_on(\"${DEP}\")\n")
      endforeach()
    endif()
    if(PARSED_ARGS_LINK)
      foreach(LINK_ARG ${PARSED_ARGS_LINK})
        string(APPEND CONTENT "nvtest.directives.link(\"${LINK_ARG}\")\n")
      endforeach()
    endif()
    string(APPEND CONTENT "def test():\n    cmd = nvtest.Executable(\"${PROGRAM}\")\n")
    list(LENGTH COMMAND_LIST N)
    if(N GREATER 1)
      list(SUBLIST COMMAND_LIST 1 -1 SUBTMP)
      join_quoted("${SUBTMP}" TMP)
      string(APPEND CONTENT "    args = [${TMP}]\n")
      string(APPEND CONTENT "    cmd(*args, allow_failure=True)\n")
    else()
      string(APPEND CONTENT "    cmd(allow_failure=True)\n")
    endif()
    string(APPEND CONTENT "    if cmd.returncode != 0:\n")
    string(APPEND CONTENT "        raise nvtest.TestFailed(\"${NAME}\")\n    return 0\n")
    string(APPEND CONTENT "if __name__ == \"__main__\":\n    sys.exit(test())\n")
    file(GENERATE OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${NAME}.pyt" CONTENT "${CONTENT}")
  else()
    get_filename_component(F ${PARSED_ARGS_SCRIPT} NAME)
    file(GENERATE OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${F}.pyt" INPUT "${PARSED_ARGS_SCRIPT}")
  endif()
endfunction()

# --- ADD_NVTEST --------------------------------------------------------------------- #
function(add_parallel_nvtest)
  macro(_print_usage)
    set(USAGE_STR "\nADD_PARALLEL_NVTEST(NAME <name> COMMAND <command> NPROC <nproc1 [nproc2...]> ")
    string(APPEND USAGE_STR "[NO_DEFAULT_LINK] [LINK link1 [link2...]])\n")
    string(APPEND USAGE_STR "[KEYWORDS kwd1 [kwd1...]])\n")
    string(APPEND USAGE_STR "Add a nvtest unit test.\n")
    message("${USAGE_STR}")
  endmacro(_print_usage)
  if(NOT MPI_FOUND)
    message(FATAL_ERROR "add_parallel_nvtest requires MPI")
  endif()
  cmake_parse_arguments(PARSED_ARGS "NO_DEFAULT_LINK" "NAME;COMMAND" "LINK;KEYWORDS;NPROC" ${ARGN})
  if(NOT PARSED_ARGS_NAME)
    _print_usage()
    message(FATAL_ERROR "Must define <name>")
  endif()
  set(NAME ${PARSED_ARGS_NAME})

  if(NOT PARSED_ARGS_COMMAND)
    _print_usage()
    message(FATAL_ERROR "Must define <command>")
  endif()

  if(NOT PARSED_ARGS_NPROC)
    _print_usage()
    message(FATAL_ERROR "Must define NPROC")
  endif()

  set(CONTENT "#!/usr/bin/env python3\nimport sys\nimport nvtest\n")
  separate_arguments(COMMAND_LIST UNIX_COMMAND ${PARSED_ARGS_COMMAND})
  if(PARSED_ARGS_KEYWORDS)
    join_quoted("${PARSED_ARGS_KEYWORDS}" KWDS)
    string(APPEND CONTENT "nvtest.directives.keywords(${KWDS})\n")
  endif()
  if(NOT NO_DEFAULT_LINK)
    list(GET COMMAND_LIST 0 PROGRAM)
    string(APPEND CONTENT "nvtest.directives.link(\"${PROGRAM}\")\n")
  endif()
  if(PARSED_ARGS_LINK)
    foreach(LINK_ARG ${PARSED_ARGS_LINK})
      string(APPEND CONTENT "nvtest.directives.link(\"${LINK_ARG}\")\n")
    endforeach()
  endif()
  if(MPIEXEC_EXECUTABLE_OVERRIDE)
    set(MPIRUN "${MPIEXEC_EXECUTABLE_OVERRIDE}")
  else()
    set(MPIRUN "${MPIEXEC_EXECUTABLE}")
  endif()
  if(MPIEXEC_NUMPROC_FLAG_OVERRIDE)
    set(MPIRUN_NP_FLAG "${MPIEXEC_NUMPROC_FLAG_OVERRIDE}")
  else()
    set(MPIRUN_NP_FLAG "${MPIEXEC_NUMPROC_FLAG}")
  endif()
  list(JOIN PARSED_ARGS_NPROC ", " NPROC)
  string(APPEND CONTENT "nvtest.directives.parameterize(\"np\", [${NPROC}])\n")
  string(APPEND CONTENT "def test():\n")
  string(APPEND CONTENT "    self = nvtest.test.instance\n")
  string(APPEND CONTENT "    mpi = nvtest.Executable(\"${MPIRUN}\")\n")
  string(APPEND CONTENT "    args = [\"${MPIRUN_NP_FLAG}\", str(self.parameters.np), ")
  join_quoted("${COMMAND_LIST}" TMP)
  string(APPEND CONTENT "${TMP}]\n")
  string(APPEND CONTENT "    mpi(*args, allow_failure=True)\n    if mpi.returncode != 0:\n")
  string(APPEND CONTENT "        raise nvtest.TestFailed(\"${NAME}\")\n    return 0\n")
  string(APPEND CONTENT "if __name__ == \"__main__\":\n    sys.exit(test())\n")
  file(GENERATE OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${NAME}.pyt" CONTENT "${CONTENT}")
endfunction()

# --- ADD_NVTEST_OPTIONS ------------------------------------------------------------- #
function(add_nvtest_options)
  macro(_print_usage)
    message("\nADD_NVTEST_OPTIONS(ON_OPTION <option>)\n Add option to nvtest options.\n")
  endmacro(_print_usage)
  cmake_parse_arguments(PARSED_ARGS "" "" "ON_OPTION" ${ARGN})
  if(NOT PARSED_ARGS_ON_OPTION)
    _print_usage()
    message(FATAL_ERROR "Must define <option>")
  endif()
  foreach(ON_OPTION ${PARSED_ARGS_ON_OPTION})
    list(APPEND NVTEST_ON_OPTIONS "${ON_OPTION}")
    list(REMOVE_DUPLICATES NVTEST_ON_OPTIONS)
  endforeach()
  set(NVTEST_ON_OPTIONS "${NVTEST_ON_OPTIONS}" CACHE STRING "" FORCE)
endfunction()

# --- ADD_NVTEST_TARGET -------------------------------------------------------------- #
function(add_nvtest_target)
  add_custom_target(
    nvtest
    VERBATIM
    WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
    COMMAND nvtest run -w .
  )
  set_target_properties(nvtest PROPERTIES EXCLUDE_FROM_ALL TRUE)
endfunction()

# --- WRITE_NVTEST_CONFIG ------------------------------------------------------------ #
function(write_nvtest_config)
  string(TIMESTAMP NOW)
  set(CONTENT "[build]\n")
  string(APPEND CONTENT "project = \"${PROJECT_NAME}@${PROJECT_VERSION}\"\n")
  string(APPEND CONTENT "type = \"${CMAKE_BUILD_TYPE}\"\n")
  string(APPEND CONTENT "date = \"${NOW}\"\n")
  string(APPEND CONTENT "[build:compiler]\n")
  string(APPEND CONTENT "vendor = \"${CMAKE_C_COMPILER_ID}\"\n")
  string(APPEND CONTENT "version = \"${CMAKE_C_COMPILER_VERSION}\"\n")
  string(APPEND CONTENT "[build:compiler:paths]\n")
  if(DEFINED ENV{SPACK_CC})
    string(APPEND CONTENT "cc = \"$ENV{SPACK_CC}\"\n")
    string(APPEND CONTENT "cxx = \"$ENV{SPACK_CXX}\"\n")
    if(DEFINED ENV{SPACK_FC})
      string(APPEND CONTENT "fc = \"$ENV{SPACK_FC}\"\n")
    endif()
  else()
    string(APPEND CONTENT "cc = \"${CMAKE_C_COMPILER}\"\n")
    string(APPEND CONTENT "cxx = \"${CMAKE_CXX_COMPILER}\"\n")
    if(CMAKE_Fortran_COMPILER)
      string(APPEND CONTENT "fc = \"${CMAKE_Fortran_COMPILER}\"\n")
    endif()
  endif()
  if(NVTEST_ON_OPTIONS)
    string(APPEND CONTENT "[build:options]\n")
    foreach(OPT ${NVTEST_ON_OPTIONS})
      string(APPEND CONTENT "${OPT} = true\n")
    endforeach()
  endif()
  if(NVTEST_OFF_OPTIONS)
    string(APPEND CONTENT "[build:options]\n")
    foreach(OPT ${NVTEST_OFF_OPTIONS})
      string(APPEND CONTENT "${OPT} = false\n")
    endforeach()
  endif()
  file(GENERATE OUTPUT "${CMAKE_BINARY_DIR}/nvtest.cfg" CONTENT "${CONTENT}")
endfunction()
